buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.wisepersist:gwt-gradle-plugin:1.0.6'
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'groovy'
    id 'idea'
    id 'org.springframework.boot' version '2.0.1.RELEASE'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'gwt'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8
def GWT_VERSION = '2.8.2'
ext['spock.version'] = '1.1'

repositories {
    jcenter()
}

sourceSets {
    main.java.srcDir "src/main/java"
    main.resources.srcDir "src/main/resources"
    test.java.srcDir "src/test/java"
    test.groovy.srcDir "src/test/groovy"
    test.resources.srcDir "src/test/resources"
}

dependencies {
    // although the gwt plugin should already download the gwt dependencies, we have to add
    // them manually as some jetty dependencies are missing otherwise
    compileOnly("com.google.gwt:gwt-user:${GWT_VERSION}")
    compileOnly("com.google.gwt:gwt-dev:${GWT_VERSION}")
    compileOnly('org.fusesource.restygwt:restygwt:2.2.3')

    compile('javax.ws.rs:javax.ws.rs-api:2.1')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-jetty')
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }

    runtime('com.h2database:h2')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.codehaus.groovy:groovy-all:2.4.15')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')

    annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

gwt {
    gwtVersion = GWT_VERSION
    modules 'com.codecrafters.SpringBootGwt'
    maxHeapSize = "1024M"
}

compileJava.dependsOn(processResources)

bootJar.dependsOn compileGwt
bootJar {
    baseName = 'spring-boot-gwt'
    version = '1.0.0'

    into('BOOT-INF/classes/static') {
        from compileGwt.outputs
    }
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}
